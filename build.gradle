plugins {
    id 'java'
    id 'jacoco'
    id 'application'
    id 'org.beryx.jlink' version '2.21.4'
    id "com.github.johnrengelman.shadow" version "6.0.0"
}

group = 'us.deathmarine.luyten'

version = '0.5.36'

mainClassName = 'us.deathmarine.luyten.Luyten'

dependencies {
    testImplementation 'junit:junit:4.13'

    implementation 'com.fifesoft:rsyntaxtextarea:3.1.1'

    implementation 'org.bitbucket.mstrobel:procyon-core:0.5.36'
    implementation 'org.bitbucket.mstrobel:procyon-expressions:0.5.36'
    implementation 'org.bitbucket.mstrobel:procyon-reflection:0.5.36'
    implementation 'org.bitbucket.mstrobel:procyon-compilertools:0.5.36'

    implementation 'com.apple:AppleJavaExtensions:1.4'

    implementation 'com.github.MinnDevelopment:java-discord-rpc:v2.0.1'

    //Look and Feel
    implementation 'com.formdev:flatlaf:0.40'
}

repositories {
    jcenter()
    maven {
        name = "Jitpack"
        url = "https://jitpack.io"
    }
}

jar {
    from sourceSets.main.output

    manifest {
        attributes([
                "Specification-Title"     : "${rootProject.name}",
                "Specification-Vendor"    : "deathmarine",
                "Specification-Version"   : "${archiveVersion}",
                "Implementation-Title"    : "${rootProject.name}",
                "Implementation-Version"  : "${archiveVersion}",
                "Implementation-Vendor"   : "deathmarine",
                "Main-Class"              : "${mainClassName}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jpackage {
    doFirst {
        project.getProperty('installerType') // throws exception if its missing
    }
}